sct = { SOI ~ NL? ~ section+ ~ EOI}

section = _{
  info_section
  | vor_section
  | ndb_section
  | fixes_section
  | airport_section
  | runway_section
  | high_airway_section
  | low_airway_section
  | unparsed_section
  | colour_definition
}

line = _{ broken ~ NL }

section_header = _{ "[" ~ header_name ~ "]" }

info_section = {
  "[INFO]" ~ NL ~ colour_definition* ~ sector_name ~ NL ~ sector_callsign ~ NL ~ designator ~ NL
  ~ coord_part ~ NL ~ coord_part ~ NL ~ MAYBE_DECIMAL ~ NL ~ MAYBE_DECIMAL ~ NL ~ MAYBE_DECIMAL ~ NL
  ~ (MAYBE_DECIMAL ~ NL)? ~ colour_definition*
}
vor_section = { "[VOR]" ~ NL ~ colour_definition* ~ (location ~ NL?)* ~ colour_definition* }
ndb_section = { "[NDB]" ~ NL ~ colour_definition* ~ ((location | broken) ~ NL)* ~ colour_definition* }
fixes_section = { "[FIXES]" ~ NL ~ colour_definition* ~ ((fix | broken) ~ NL)* ~ colour_definition* }
airport_section = { "[AIRPORT]" ~ NL ~ colour_definition* ~ (aerodrome ~ NL?)* ~ colour_definition* }
runway_section = { "[RUNWAY]" ~ NL ~ colour_definition* ~ (runway ~ NL)* ~ colour_definition* }
high_airway_section = { "[HIGH AIRWAY]" ~ NL ~ colour_definition* ~ (airway ~ NL)* ~ colour_definition* }
low_airway_section = { "[LOW AIRWAY]" ~ NL ~ colour_definition* ~ (airway ~ NL)* ~ colour_definition* }
unparsed_section = { (section_header ~ NL ~ line*) }

sector_name = @{ (LETTER | ASCII_DIGIT | "\t" | ' '..'~')+ }
sector_callsign = @{ (ASCII_ALPHANUMERIC | "_")+ }


colour_definition = { "#define" ~ colour_name ~ colour_value ~ NL }
colour_name = @{ (ASCII_ALPHANUMERIC | "-" | "_")+ }
colour_value = @{ ASCII_DIGIT+ }

fix = { fix_designator ~ coordinate }
aerodrome = { designator ~ (frequency | ".")? ~ coordinate ~ ("A" | "B" | "C" | "D" | "E" | "F" | "G") }
location = { designator ~ frequency ~ coordinate }
runway = { runway_designator ~ runway_designator ~ degrees ~ degrees ~ coordinate ~ coordinate ~ designator ~ (!NEWLINE ~ ANY)* }
airway = { fix_designator ~ (coordinate | airway_fix) ~ (coordinate | airway_fix) }
// EDGG has invalid WPT with spaces
broken = @{ !("[" | "#") ~ (!NL ~ ANY)* }

airway_fix = { (designator ~ designator) }
coordinate = { coord_part ~ coord_part }
degrees = @{ ASCII_DIGIT+ }
frequency = @{ MAYBE_DECIMAL }
runway_designator = @{ ASCII_DIGIT{2} ~ ("L" | "C" | "R" | "G")? }

coord_part = ${ hemi ~ deg ~ "." ~ min ~ "." ~ sec }

fix_designator = @{ (LETTER | ASCII_DIGIT | "(" | ")" | "/" | "-" | "_" | "." | "[" | "]")+ }
designator = @{ (ASCII_ALPHA_UPPER | ASCII_DIGIT)+ }

header_name = @{ (ASCII_ALPHA_UPPER | WHITESPACE)+ }

deg = @{ ASCII_DIGIT+ }
min = @{ ASCII_DIGIT+ }
sec = @{ DECIMAL }
hemi = @{ "N" | "S" | "W" | "E" }

DECIMAL = @{ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+ }
MAYBE_DECIMAL = @{ "-"? ~ (DECIMAL | ASCII_DIGIT+) }

NL = _{ NEWLINE+ }
COMMENT = _{ (";" | "//") ~ (!NEWLINE ~ ANY)* }
WHITESPACE = _{ " " | "\t" }
