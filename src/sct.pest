sct = { SOI ~ NL? ~ (color_definition ~ NL)* ~ section+ ~ EOI}

section = _{
  vor_section
  | ndb_section
  | fixes_section
  | airport_section
  | runway_section
  | high_airway_section
  | low_airway_section
  | unparsed_section
}

line = _{ broken ~ NL }

section_header = _{ "[" ~ header_name ~ "]" }

vor_section = { "[VOR]" ~ NL ~ (color_definition ~ NL)* ~ (location ~ NL)* ~ (color_definition ~ NL)* }
ndb_section = { "[NDB]" ~ NL ~ (color_definition ~ NL)* ~ (location ~ NL)* ~ (color_definition ~ NL)* }
fixes_section = { "[FIXES]" ~ NL ~ (color_definition ~ NL)* ~ ((fix | broken) ~ NL)* ~ (color_definition ~ NL)* }
airport_section = { "[AIRPORT]" ~ NL ~ (color_definition ~ NL)* ~ (location ~ NL)* ~ (color_definition ~ NL)* }
runway_section = { "[RUNWAY]" ~ NL ~ (color_definition ~ NL)* ~ (runway ~ NL)* ~ (color_definition ~ NL)* }
high_airway_section = { "[HIGH AIRWAY]" ~ NL ~ (color_definition ~ NL)* ~(airway ~ NL)* ~ (color_definition ~ NL)* }
low_airway_section = { "[LOW AIRWAY]" ~ NL ~ (color_definition ~ NL)* ~ (airway ~ NL)* ~ (color_definition ~ NL)* }
unparsed_section = { (section_header ~ NL ~ line*) }

color_definition = { "#define" ~ color_name ~ color_value }
color_name = @{ (ASCII_ALPHANUMERIC | "-" | "_")+ }
color_value = @{ ASCII_DIGIT+ }

fix = { fix_designator ~ coordinate }
location = { designator ~ frequency ~ coordinate ~ ("A" | "B" | "C" | "D" | "E" | "F" | "G")? }
runway = { runway_designator ~ runway_designator ~ degrees ~ degrees ~ coordinate ~ coordinate ~ designator }
airway = { designator ~ coordinate ~ coordinate }
// EDGG has invalid WPT with spaces
broken = @{ !("[" | "#") ~ (!NL ~ ANY)* }

coordinate = { coord_part ~ coord_part }
degrees = @{ ASCII_DIGIT+ }
frequency = @{ DECIMAL }
runway_designator = @{ ASCII_DIGIT{2} ~ ("L" | "C" | "R")? }

coord_part = ${ hemi ~ deg ~ "." ~ min ~ "." ~ sec }

fix_designator = @{ (LETTER | ASCII_DIGIT | "(" | ")" | "/" | "-" | "_" | ".")+ }
designator = @{ (ASCII_ALPHA_UPPER | ASCII_DIGIT)+ }

header_name = @{ (ASCII_ALPHA_UPPER | WHITESPACE)+ }

deg = @{ ASCII_DIGIT+ }
min = @{ ASCII_DIGIT+ }
sec = @{ DECIMAL }
hemi = @{ "N" | "S" | "W" | "E" }

DECIMAL = @{ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+ }

NL = _{ NEWLINE+ }
COMMENT = _{ ";" ~ (!NEWLINE ~ ANY)* ~ NL }
WHITESPACE = _{ " " | "\t" }
