symbols = { SOI ~ NL? ~ symbol+ ~ EOI}
maps = { SOI ~ NL? ~ (map | symboldef | colourdef | linestyledef | override_sct)+ ~ EOI }

symbol = {
  "SYMBOL:" ~ name ~ NL ~ symbolrule+
}
symboldef = {
  "SYMBOLDEF:" ~ name ~ NL ~ symbolrule+
}

colourdef = {
  "COLORDEF:" ~ name ~ ":" ~ number ~ ":" ~ number ~ ":" ~ number ~ NL?
}

linestyledef = {
  "LINESTYLEDEF:" ~ name ~ ":" ~ name ~ ":" ~ name ~ (":" ~ (number ~ ("," ~ number)*))? ~ NL?
}

override_sct = { "OVERRIDE_SCT_MAP:" ~ override_folder ~ ("\\" ~ name)? ~ NL? }
override_folder = { (!("\\" | NL) ~ ANY)+ }

// TODO DEFAULT_SCREEN-SPECIFIC, DEFAULT_GLOBAL

map = { "MAP:" ~ name ~ NL ~ map_setting+ }

// MAP
map_setting = _{
  (" " | "\t")* ~ (
    folder | colour | asrdata | active | andactive | layer | mapsymbol | fontstyle
    | fontsize | zoom | style | mapline | text | global | screen_specific
    | circle | coordline | coordpoly | coord | textalign | override_sct
    | hidden
  )
  ~ NL?
}

folder = { "FOLDER:" ~ name }

colour = { "COLOR:" ~ name }

asrdata = { "ASRDATA:" ~ (wildcard | names) }

active = { "ACTIVE:" ~ active_options }
andactive = { "AND_ACTIVE:" ~ active_options }
active_options = _{
  active_sched | active_id | active_notam | active_aup | active_area_with_excludes
  | active_area | active_callsign | active_rwy_with_excludes | active_rwy | active_always
  | active_map
}
active_always = { "1" }
active_id = {
  "ID:" ~ (wildcard | names) ~ ":" ~ (wildcard | names) ~ ":"
  ~ (wildcard | names) ~ ":" ~ (wildcard | names)
}
active_callsign = {
  "CALLSIGN:" ~ (wildcard | names) ~ ":" ~ (wildcard | names) ~ ":"
  ~ (wildcard | names) ~ ":" ~ (wildcard | names)
}
active_area = {
  "AREA:" ~ names
}
active_area_with_excludes = {
  "AREA:" ~ names ~ ":" ~ names
}
active_aup = {
  "AUP:" ~ names
}
active_map = {
  "MAP:" ~ active_map_operator ~ ":" ~ name_no_backslash ~ "\\" ~ name
}
active_notam = {
  "NOTAM:" ~ icao ~ ":" ~ names
}
active_rwy = {
  "RWY:ARR:" ~ (wildcard | runways) ~ ":DEP:" ~ (wildcard | runways)
}
active_rwy_with_excludes = {
  "RWY:ARR:" ~ (wildcard | runways) ~ ":" ~ (wildcard | runways)
  ~ ":DEP:" ~ (wildcard | runways) ~ ":" ~ (wildcard | runways)
}
runways = { runway ~ ("," ~ runway)* }
active_sched = { (ymd | md) ~ ":" ~ (ymd | md) ~ ":" ~ ASCII_DIGIT+ ~ ":" ~ hm ~ ":" ~ hm }
active_map_operator = { "!" | "=" }

layer = { "LAYER:" ~ number }

mapsymbol = { "SYMBOL" ~ ("/" ~ textalign_config)? ~ ":" ~ name ~ ":" ~ (coordinate | name) ~ (":" ~ mapsymbollabel)? }
mapsymbollabel = { name ~ ":" ~ point }

fontstyle = { "FONTSTYLE:" ~ (fontstyle_set | fontstyle_default) }
fontstyle_default = { "0" }
fontstyle_set = { number ~ ":" ~ boolean ~ ":" ~ boolean ~ ":" ~ boolean }
fontsize = { "FONTSIZE:" ~ ((font_symbol ~ ":" ~ decimal) | fontsize_default)}
fontsize_default = { "0" }
font_symbol = { "=" | "+" | "-" | "*" }

zoom = { "ZOOM:" ~ decimal }

style = { "STYLE:" ~ name ~ (":" ~ number)? }

mapline = { ("LINE:" ~ (maplinecoordinates | maplinewpts) ~ NL)+ }
maplinecoordinates = { coordinate ~ ":" ~ coordinate }
maplinewpts = { name ~ ":" ~ name }

text = { "TEXT" ~ ("/" ~ textalign_config)? ~ ":" ~ (coordinate | name) ~ ":" ~ text_content }
text_content = { (!NL ~ ANY)+ }

textalign = { "TEXTALIGN:" ~ textalign_config }
textalign_config = { horizontalalign? ~ verticalalign? }
horizontalalign = { "L" | "C" | "R" }
verticalalign = { "T" | "C" | "B" }

screen_specific = { "SCREEN-SPECIFIC" }
global = { "GLOBAL" }

hidden = { "HIDDEN" }

circle = { "CIRCLE:" ~ coordinate ~ ":" ~ decimal ~ ":" ~ decimal }

coord = { "COORD:" ~ (coordinate | name) }

coordline = { "COORDLINE" }
coordpoly = { "COORDPOLY:" | number }

// SYMBOLDEF
symbolrule = _{ (
  moveto | line | fillarc_ellipse | fillarc | arc_ellipse | arc | polygon | pixel | ellipse | ellipse_circle | fillrect
) ~ NL? }
moveto = { "MOVETO:" ~ point }
line = { "LINETO:" ~ point }
fillarc = {
  "FILLARC:" ~ point ~ ":" ~ decimal ~ ":" ~ number ~ ":" ~ number
}
fillarc_ellipse = {
  "FILLARC:" ~ point ~ ":" ~ decimal ~ ":" ~ decimal ~ ":" ~ number ~ ":" ~ number
}
arc = {
  "ARC:" ~ point ~ ":" ~ decimal ~ ":" ~ number ~ ":" ~ number
}
arc_ellipse = {
  "ARC:" ~ point ~ ":" ~ decimal ~ ":" ~ decimal ~ ":" ~ number ~ ":" ~ number
}
ellipse = {
  "ELLIPSE:" ~ point ~ ":" ~ decimal ~ ":" ~ decimal
}
ellipse_circle = {
  "ELLIPSE:" ~ point ~ ":" ~ decimal
}
polygon = { "POLYGON" ~ (":" ~ point)+ }
pixel = { "SETPIXEL:" ~ point }
fillrect = {
  "FILLRECT:" ~ point ~ ":" ~ point
}

// TODO: SCTDATA

name = @{ (!(NL | ":") ~ ANY)* }
name_no_comma = @{ (!(NL | ":" | ",") ~ ANY)* }
name_no_backslash = @{ (!(NL | "\\") ~ ANY)* }
names = { name_no_comma ~ ("," ~ name_no_comma)* }
wildcard = { "*" }

coordinate = { coord_part ~ ":" ~ coord_part }
coord_part = { sct_coord_part | decimal }
sct_coord_part = ${ hemi ~ deg ~ "." ~ min ~ "." ~ sec }
deg = @{ ASCII_DIGIT+ }
min = @{ ASCII_DIGIT+ }
sec = @{ decimal }
hemi = @{ "N" | "S" | "W" | "E" | "n" | "s" | "w" | "e" }

md = { ASCII_DIGIT{2} ~ ASCII_DIGIT{2} }
ymd = { ASCII_DIGIT{2} ~ ASCII_DIGIT{2} ~ ASCII_DIGIT{2} }
hm = { ASCII_DIGIT{2} ~ ASCII_DIGIT{2} }

runway = { icao ~ rwy_designator }
icao = { ASCII_ALPHA_UPPER{4} }
rwy_designator = { ASCII_DIGIT{2} ~ ("L" | "C" | "R" | "G")? }
point = { decimal ~ ":" ~ decimal }
number = @{ ("-"|"+")? ~ ASCII_DIGIT+ }
decimal = @{ ("-"|"+")? ~ ((ASCII_DIGIT* ~ "." ~ ASCII_DIGIT+) | ASCII_DIGIT+) }
boolean = { "0" | "1" }

NL = _{ ((" " | "\t")* ~ NEWLINE)+ }
// per documentation only "//" but ";" is used quite often and apparently ignored
// in the ES topsky plugin
COMMENT = _{ (" " | "\t")* ~ ("//" | ";") ~ (!NL ~ ANY)* }
