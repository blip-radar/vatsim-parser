symbols = { SOI ~ NL? ~ symbol+ ~ EOI}
maps = { SOI ~ (map | symboldef | colordef | override_sct)+ ~ EOI }

symbol = {
  "SYMBOL:" ~ name ~ NL ~ symbolrule+
}
symboldef = {
  "SYMBOLDEF:" ~ name ~ NL ~ symbolrule+
}

colordef = {
  "COLORDEF:" ~ name ~ ":" ~ number ~ ":" ~ number ~ ":" ~ number ~ NL?
}

override_sct = { "OVERRIDE_SCT_MAP:" ~ override_folder ~ name ~ NL? }
override_folder = { ((!("\\" | NL) ~ ANY)+ ~ "\\")? }

map = { "MAP:" ~ name ~ NL ~ map_setting+ }

// MAP
map_setting = _{
  (" " | "\t")* ~ (
    folder | color | asrdata | active | layer | mapsymbol
    | fontsize | zoom | style | mapline | text | screen_specific | circle
    | coordline | coordpoly | coord
  )
  ~ NL?
}

folder = { "FOLDER:" ~ name }

color = { "COLOR:" ~ name }

asrdata = { "ASRDATA:" ~ (wildcard | (name ~ ("," ~ name)*)) }

active = { "ACTIVE:" ~ (active_sched | active_id | active_rwy_with_excludes | active_rwy | active_always ) }
active_always = { "1" }
active_id = {
  "ID:" ~ (wildcard | names) ~ ":" ~ (wildcard | names) ~ ":"
  ~ (wildcard | names) ~ ":" ~ (wildcard | names)
}
active_rwy = {
  "RWY:ARR:" ~ (wildcard | runways) ~ ":DEP:" ~ (wildcard | runways)
}
active_rwy_with_excludes = {
  "RWY:ARR:" ~ (wildcard | runways) ~ ":" ~ (wildcard | runways)
  ~ ":DEP:" ~ (wildcard | runways) ~ ":" ~ (wildcard | runways)
}
runways = { runway ~ ("," ~ runway)* }
active_sched = { (ymd | md) ~ ":" ~ (ymd | md) ~ ":" ~ ASCII_DIGIT+ ~ ":" ~ hm ~ ":" ~ hm }

layer = { "LAYER:" ~ number }

mapsymbol = { "SYMBOL:" ~ name ~ ":" ~ (coordinate | name) ~ (":" ~ mapsymbollabel)? }
mapsymbollabel = { name ~ ":" ~ point }

fontsize = { "FONTSIZE:" ~ (font_symbol ~ ":" ~ number | "0")}
font_symbol = { "=" | "+" | "-" | "*" }

zoom = { "ZOOM:" ~ number }

style = { "STYLE:" ~ name ~ (":" ~ number)? }

mapline = { "LINE:" ~ (maplinecoordinates | maplinewpts) }
maplinecoordinates = { coordinate ~ ":" ~ coordinate }
maplinewpts = { name ~ ":" ~ name }

text = { "TEXT" ~ ("/" ~ textalign)? ~ ":" ~ (coordinate | name) ~ ":" ~ text_content }
text_content = { (!NL ~ ANY)+ }
textalign = { horizontalalign ~ verticalalign }
horizontalalign = { "L" | "C" | "R" }
verticalalign = { "T" | "C" | "B" }

screen_specific = { "SCREEN-SPECIFIC" }

circle = { "CIRCLE:" ~ coordinate ~ ":" ~ decimal ~ ":" ~ decimal }

coord = { "COORD:" ~ coordinate }

coordline = { "COORDLINE" }
coordpoly = { "COORDPOLY:" | number }

// SYMBOLDEF
symbolrule = _{ (moveto | line | fillarc | arc | polygon | pixel) ~ NL? }
moveto = { "MOVETO:" ~ point }
line = { "LINETO:" ~ point }
fillarc = {
  "FILLARC:" ~ point ~ ":" ~ number ~ ":" ~ number ~ ":" ~ number
}
arc = {
  "ARC:" ~ point ~ ":" ~ number ~ ":" ~ number ~ ":" ~ number
}
polygon = { "POLYGON" ~ (":" ~ point)+ }
pixel = { "SETPIXEL:" ~ point }


name = @{ (ASCII_ALPHA | ASCII_DIGIT | "_" | "-" | " " | "/" | "." | "(" | ")")+ }
names = { name ~ ("," ~ name)* }
wildcard = { "*" }

coordinate = { coord_part ~ ":" ~ coord_part }
coord_part = { decimal | sct_coord_part }
sct_coord_part = ${ hemi ~ deg ~ "." ~ min ~ "." ~ sec }
deg = @{ ASCII_DIGIT+ }
min = @{ ASCII_DIGIT+ }
sec = @{ decimal }
hemi = @{ "N" | "S" | "W" | "E" }

md = { ASCII_DIGIT{2} ~ ASCII_DIGIT{2} }
ymd = { ASCII_DIGIT{2} ~ ASCII_DIGIT{2} ~ ASCII_DIGIT{2} }
hm = { ASCII_DIGIT{2} ~ ASCII_DIGIT{2} }

runway = { icao ~ rwy_designator }
icao = { ASCII_ALPHA_UPPER{4} }
rwy_designator = { ASCII_DIGIT{2} ~ ("L" | "C" | "R")? }
point = { number ~ ":" ~ number }
number = @{ "-"? ~ ("0" | (ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)) }
decimal = @{ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? }

NL = _{ ((" " | "\t")* ~ NEWLINE)+ }
COMMENT = _{ "//" ~ (!NL ~ ANY)* ~ NL }
