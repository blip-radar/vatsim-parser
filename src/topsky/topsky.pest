symbols = { SOI ~ NL? ~ symbol+ ~ EOI}
maps = { SOI ~ (map | symboldef | colordef | override_sct)+ ~ EOI }

symbol = {
  "SYMBOL:" ~ name ~ NL ~ symbolrule+
}
symboldef = {
  "SYMBOLDEF:" ~ name ~ NL ~ symbolrule+
}

colordef = {
  "COLORDEF:" ~ name ~ ":" ~ number ~ ":" ~ number ~ ":" ~ number ~ NL?
}

override_sct = { "OVERRIDE_SCT_MAP:" ~ override_folder ~ name ~ NL? }
override_folder = { ((!("\\" | NL) ~ ANY)+ ~ "\\")? }

// TODO DEFAULT_SCREEN-SPECIFIC, DEFAULT_GLOBAL

map = { "MAP:" ~ name ~ NL ~ map_setting+ }

// MAP
map_setting = _{
  (" " | "\t")* ~ (
    folder | color | asrdata | active | layer | mapsymbol | fontstyle
    | fontsize | zoom | style | mapline | text | global | screen_specific
    | circle | coordline | coordpoly | coord
  )
  ~ NL?
}

folder = { "FOLDER:" ~ name }

color = { "COLOR:" ~ name }

asrdata = { "ASRDATA:" ~ (wildcard | (name ~ ("," ~ name)*)) }

active = { "ACTIVE:" ~ (active_sched | active_id | active_rwy_with_excludes | active_rwy | active_always ) }
andactive = { "AND_ACTIVE:" ~ (active_sched | active_id | active_rwy_with_excludes | active_rwy | active_always ) }
active_always = { "1" }
active_id = {
  "ID:" ~ (wildcard | names) ~ ":" ~ (wildcard | names) ~ ":"
  ~ (wildcard | names) ~ ":" ~ (wildcard | names)
}
active_rwy = {
  "RWY:ARR:" ~ (wildcard | runways) ~ ":DEP:" ~ (wildcard | runways)
}
active_rwy_with_excludes = {
  "RWY:ARR:" ~ (wildcard | runways) ~ ":" ~ (wildcard | runways)
  ~ ":DEP:" ~ (wildcard | runways) ~ ":" ~ (wildcard | runways)
}
runways = { runway ~ ("," ~ runway)* }
active_sched = { (ymd | md) ~ ":" ~ (ymd | md) ~ ":" ~ ASCII_DIGIT+ ~ ":" ~ hm ~ ":" ~ hm }

layer = { "LAYER:" ~ number }

mapsymbol = { "SYMBOL" ~ ("/" ~ textalign)? ~ ":" ~ name ~ ":" ~ (coordinate | name) ~ (":" ~ mapsymbollabel)? }
mapsymbollabel = { name ~ ":" ~ point }

fontstyle = { "FONTSTYLE:" ~ (fontstyle_set | fontstyle_default) }
fontstyle_default = { "0" }
fontstyle_set = { number ~ ":" ~ boolean ~ ":" ~ boolean ~ ":" ~ boolean }
fontsize = { "FONTSIZE:" ~ ((font_symbol ~ ":" ~ decimal) | fontsize_default)}
fontsize_default = { "0" }
font_symbol = { "=" | "+" | "-" | "*" }

zoom = { "ZOOM:" ~ decimal }

style = { "STYLE:" ~ name ~ (":" ~ number)? }

mapline = { "LINE:" ~ (maplinecoordinates | maplinewpts) }
maplinecoordinates = { coordinate ~ ":" ~ coordinate }
maplinewpts = { name ~ ":" ~ name }

text = { "TEXT" ~ ("/" ~ textalign)? ~ ":" ~ (coordinate | name) ~ ":" ~ text_content }
text_content = { (!NL ~ ANY)+ }
textalign = { horizontalalign ~ verticalalign? }
horizontalalign = { "L" | "C" | "R" }
verticalalign = { "T" | "C" | "B" }

screen_specific = { "SCREEN-SPECIFIC" }
global = { "GLOBAL" }

circle = { "CIRCLE:" ~ coordinate ~ ":" ~ decimal ~ ":" ~ decimal }

coord = { "COORD:" ~ coordinate }

coordline = { "COORDLINE" }
coordpoly = { "COORDPOLY:" | number }

// SYMBOLDEF
symbolrule = _{ (
  moveto | line | fillarc_ellipse | fillarc | arc_ellipse | arc | polygon | pixel | ellipse | ellipse_circle
) ~ NL? }
moveto = { "MOVETO:" ~ point }
line = { "LINETO:" ~ point }
fillarc = {
  "FILLARC:" ~ point ~ ":" ~ decimal ~ ":" ~ number ~ ":" ~ number
}
fillarc_ellipse = {
  "FILLARC:" ~ point ~ ":" ~ decimal ~ ":" ~ decimal ~ ":" ~ number ~ ":" ~ number
}
arc = {
  "ARC:" ~ point ~ ":" ~ decimal ~ ":" ~ number ~ ":" ~ number
}
arc_ellipse = {
  "ARC:" ~ point ~ ":" ~ decimal ~ ":" ~ decimal ~ ":" ~ number ~ ":" ~ number
}
ellipse = {
  "ELLIPSE:" ~ point ~ ":" ~ decimal ~ ":" ~ decimal
}
ellipse_circle = {
  "ELLIPSE:" ~ point ~ ":" ~ decimal
}
polygon = { "POLYGON" ~ (":" ~ point)+ }
pixel = { "SETPIXEL:" ~ point }

// TODO: SCTDATA

name = @{ (!(NL | ":") ~ ANY)+ }
names = { name ~ ("," ~ name)* }
wildcard = { "*" }

coordinate = { coord_part ~ ":" ~ coord_part }
coord_part = { decimal | sct_coord_part }
sct_coord_part = ${ hemi ~ deg ~ "." ~ min ~ "." ~ sec }
deg = @{ ASCII_DIGIT+ }
min = @{ ASCII_DIGIT+ }
sec = @{ decimal }
hemi = @{ "N" | "S" | "W" | "E" }

md = { ASCII_DIGIT{2} ~ ASCII_DIGIT{2} }
ymd = { ASCII_DIGIT{2} ~ ASCII_DIGIT{2} ~ ASCII_DIGIT{2} }
hm = { ASCII_DIGIT{2} ~ ASCII_DIGIT{2} }

runway = { icao ~ rwy_designator }
icao = { ASCII_ALPHA_UPPER{4} }
rwy_designator = { ASCII_DIGIT{2} ~ ("L" | "C" | "R")? }
point = { decimal ~ ":" ~ decimal }
number = @{ ("-"|"+")? ~ ASCII_DIGIT+ }
decimal = @{ ("-"|"+")? ~ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? }
boolean = { "0" | "1" }

NL = _{ ((" " | "\t")* ~ NEWLINE)+ }
// per documentation only "//" but ";" is used quite often and apparently ignored
// in the ES topsky plugin
COMMENT = _{ (" " | "\t")* ~ ("//" | ";") ~ (!NL ~ ANY)* ~ NL }
