asr = { SOI ~ NL? ~ setting+ ~ EOI}

setting = _{
  (above
  | below
  | disable_panning
  | disable_zooming
  | display_rotation
  | display_type
  | display_type_geo_referenced
  | display_type_need_radar_content
  | history_dots
  | leader
  | plugin
  | sector_file
  | sector_title
  | show_leader
  | show_c
  | show_standby
  | simulation_mode
  | tag_family
  | turn_leader
  | window_area
  | free_text
  | airports
  | fixes
  | ndbs
  | vors
  | low_airways
  | high_airways
  | sids
  | stars
  | geo
  | ground_network
  | regions
  | artcc_boundary
  | artcc_low_boundary
  | artcc_high_boundary
  | runways)
  ~ NL?
}

above = { "ABOVE:" ~ INTEGER }
below = { "BELOW:" ~ INTEGER }
disable_panning = { "DISABLEPANNING:" ~ BOOL }
disable_zooming = { "DISABLEZOOMING:" ~ BOOL }
display_rotation = { "DisplayRotation:" ~ DECIMAL }
display_type = { "DisplayTypeName:" ~ value }
display_type_geo_referenced = { "DisplayTypeGeoReferenced:" ~ BOOL }
display_type_need_radar_content = { "DisplayTypeNeedRadarContent:" ~ BOOL }
history_dots = { "HISTORY_DOTS:" ~ INTEGER }
leader = { "LEADER:" ~ INTEGER }
sector_file = { "SECTORFILE:" ~ value }
sector_title = { "SECTORTITLE:" ~ value }
show_leader = { "SHOWLEADER:" ~ BOOL }
show_c = { "SHOWC:" ~ BOOL }
show_standby = { "SHOWSB:" ~ BOOL }
simulation_mode = { "SIMULATION_MODE:" ~ INTEGER }
tag_family = { "TAGFAMILY:" ~ value }
turn_leader = { "TURNLEADER:" ~ BOOL }
window_area = { "WINDOWAREA:" ~ coordinate ~ ":" ~ coordinate }

plugin = { "PLUGIN:" ~ multivalue ~ ":" ~ multivalue ~ ":" ~ value }

runways = { "Runways:" ~ runway ~ ":" ~ runway_type }
airports = { "Airports:" ~ multivalue ~ ":" ~ fix_type }
ndbs = { "NDBs:" ~ multivalue ~ ":" ~ navaid_type }
vors = { "VORs:" ~ multivalue ~ ":" ~ navaid_type }
fixes = { "Fixes:" ~ multivalue ~ ":" ~ fix_type }
low_airways = { "Low airways:" ~ multivalue ~ ":" ~ airway_type }
high_airways = { "High airways:" ~ multivalue ~ ":" ~ airway_type }
sids = { "Sids:" ~ multivalue ~ ":"  }
stars = { "Stars:" ~ multivalue ~ ":" }
free_text = { "Free Text:" ~ multivalue_backslash ~ "\\" ~ multivalue ~ ":freetext" }
geo = { "Geo:" ~ multivalue ~ ":" }
ground_network = { "Ground Network:" ~ multivalue ~ ":" ~ ground_network_type }
regions = { "Regions:" ~ multivalue ~ ":polygon"  }
artcc_boundary = { "ARTCC boundary:" ~ multivalue ~ ":" }
artcc_low_boundary = { "ARTCC low boundary:" ~ multivalue ~ ":" }
artcc_high_boundary = { "ARTCC high boundary:" ~ multivalue ~ ":" }

// EDGG has invalid WPT with spaces
broken = @{ !("[") ~ (!NL ~ ANY)* }

coordinate = _{ coord_part ~ ":" ~ coord_part }
coord_part = { DECIMAL | sct_coord_part }
sct_coord_part = ${ hemi ~ deg ~ "." ~ min ~ "." ~ sec }

navaid_type = _{ symbol | name | frequency }
fix_type = _{ symbol | name }
airway_type = _{ line | name }
runway_type = _{
  centerline | name | ext_centerline
}
ground_network_type = _{ exit | taxiway | terminal_taxiway }

centerline = { "centerline" }
ext_centerline = {
  "extended centerline 1 left ticks" | "extended centerline 1 left vectoring" | "extended centerline 1 left base"
  | "extended centerline 1 right ticks" | "extended centerline 1 right vectoring" | "extended centerline 1 right base"
  | "extended centerline 1"
  | "extended centerline 2 left ticks" | "extended centerline 2 left vectoring" | "extended centerline 2 left base"
  | "extended centerline 2 right ticks" | "extended centerline 2 right vectoring" | "extended centerline 2 right base"
  | "extended centerline 2"
}
name = { "name" }
frequency = { "frequency" }
symbol = { "symbol" }
line = { "line" }
exit = { "exit" }
taxiway = { "taxiway" }
terminal_taxiway = { "terminal taxiway" }

runway = _{ airport ~ " " ~ runway_desig ~ "-" ~ runway_desig }
airport = @{ (!(" " ~ runway_desig) ~ ANY)* }
runway_desig = @{ ASCII_DIGIT{2} ~ ("L" | "R" | "C" | "W" | "G")? }

value = @{ (!NL ~ ANY)* }
multivalue = @{ (!(NL|":") ~ ANY)* }
multivalue_backslash = @{ (!"\\" ~ ANY)* }

deg = @{ ASCII_DIGIT+ }
min = @{ ASCII_DIGIT+ }
sec = @{ DECIMAL }
hemi = @{ "N" | "S" | "W" | "E" }

INTEGER = @{ "-"? ~ ASCII_DIGIT+ }
DECIMAL = @{ "-"? ~ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? }
BOOL = { "0" | "1" }

NL = _{ NEWLINE+ }
COMMENT = _{ ";" ~ (!NEWLINE ~ ANY)* ~ NL }
