ese = { SOI ~ NL? ~ section+ ~ EOI}

section = _{
  position_section
  | airspace_section
  | sidsstars_section
  | unparsed_section
}

line = _{ broken ~ NL }

section_header = _{ "[" ~ header_name ~ "]" }

position_section = { "[POSITIONS]" ~ NL ~ (position ~ NL)* }
airspace_section = { "[AIRSPACE]" ~ NL ~ (sectorline | circle_sectorline | display_sectorline | sector | cop | fir_cop | msaw)* }
sidsstars_section = { "[SIDSSTARS]" ~ NL ~ ((sid | star) ~ NL)* }
unparsed_section = { (section_header ~ NL ~ line*) }

position = {
  colon_delimited_text ~ ":" ~ colon_delimited_text ~ ":" ~ frequency ~ ":" ~ colon_delimited_text ~ ":" ~ colon_delimited_text ~ ":" ~ colon_delimited_text ~ ":"
  ~ colon_delimited_text ~ ":" ~ colon_delimited_text ~ ":" ~ colon_delimited_text ~ ":" ~ squawk_range? ~ ":"?
  ~ coordinate? ~ (":" ~ coordinate)? ~ (":" ~ coordinate)? ~ (":" ~ coordinate)?
}
squawk_range = { integer ~ ":" ~ integer }

cop = {
  "COPX:" ~ prev ~ ":" ~ dep_rwy ~ ":" ~ cop_def ~ ":" ~ after ~ ":" ~ arr_rwy ~ ":" ~ sctx ~ ":"
  ~ sctn ~ ":" ~ alt_clb ~ ":" ~ alt_des ~ ":" ~ cop_description ~ NL
}
fir_cop = {
  "FIR_COPX:" ~ prev ~ ":" ~ dep_rwy ~ ":" ~ cop_def ~ ":" ~ after ~ ":" ~ arr_rwy ~ ":" ~ sctx ~ ":"
  ~ sctn ~ ":" ~ alt_clb ~ ":" ~ alt_des ~ ":" ~ cop_description ~ NL
}
prev = _{ wildcard | colon_delimited_text }
dep_rwy = _{ wildcard | designator }
after = _{ wildcard | colon_delimited_text }
arr_rwy = _{ wildcard | designator }
cop_def = _{ wildcard | colon_delimited_text }
sctx = _{ colon_delimited_text }
sctn = _{ colon_delimited_text }
alt_clb = _{ wildcard | integer }
alt_des = _{ wildcard | integer }
cop_description = _{ colon_delimited_text }

sectorline = { "SECTORLINE:" ~ colon_delimited_text ~ NL ~ ((display | coord) ~ NL)* }
display = { "DISPLAY:" ~ colon_delimited_text ~ ":" ~ colon_delimited_text ~ ":" ~ colon_delimited_text }

circle_sectorline = {
  "CIRCLE_SECTORLINE:" ~ colon_delimited_text ~ ":" ~ fix_designator ~ ":" ~ decimal ~ NL
  ~ (display ~ NL)*
}

display_sectorline = { "DISPLAY_SECTORLINE:" ~ colon_delimited_text ~ ":" ~ colon_delimited_text ~ ":" ~ colon_delimited_text ~ ":" ~ colon_delimited_text ~ NL }

sector = { "SECTOR:" ~ colon_delimited_text ~ ":" ~ integer ~ ":" ~ integer ~ NL ~ (sector_subsettings ~ NL)* }
sector_subsettings = _{ owner | altowner | border | guest | active | depapt | arrapt }
owner = { "OWNER" ~ (":" ~ colon_delimited_text)+ }
altowner = { "ALTOWNER:" ~ colon_delimited_text ~ (":" ~ colon_delimited_text)+ }
border = { "BORDER" ~ (":" ~ colon_delimited_text)+ }
guest = { "GUEST" ~ ":" ~ sct ~ ":" ~ adep ~ ":" ~ ades  }
active = { "ACTIVE:" ~ designator ~ ":" ~ designator }
depapt = { "DEPAPT" ~ (":" ~ designator)+ }
arrapt = { "ARRAPT" ~ (":" ~ designator)+ }
sct = _{ colon_delimited_text }
adep = _{ wildcard | designator }
ades = _{ wildcard | designator }

msaw = { "MSAW:" ~ colon_delimited_text ~ ":" ~ integer ~ NL ~ (coord ~ NL)+ }

sid = { "SID:" ~ designator ~ ":" ~ (runway_designator | none) ~ ":" ~ colon_delimited_text ~ ":" ~ route }
star = { "STAR:" ~ designator ~ ":" ~ (runway_designator | none) ~ ":" ~ colon_delimited_text ~ ":" ~ route }

wildcard = { "*" }

broken = @{ !("[") ~ (!NL ~ ANY)* }

coord = _{ "COORD:" ~ coordinate }

frequency = @{ decimal }


route = { designator* }
fix_designator = @{ (LETTER | ASCII_DIGIT | "(" | ")" | "/" | "-" | "_" | ".")+ }
designator = @{ (ASCII_ALPHA_UPPER | ASCII_DIGIT | "-")+ }
pos_designator = @{ (ASCII_ALPHA_UPPER | ASCII_DIGIT | "_" | "-")+ }
none = { "" }

header_name = @{ (ASCII_ALPHA_UPPER | WHITESPACE)+ }

COMMENT = _{ ";" ~ (!NEWLINE ~ ANY)* }
WHITESPACE = _{ " " | "\t" }
