sct = { SOI ~ section? ~ (NL ~ section)* ~ NL? ~ EOI }

section = _{
  info_section
  | vor_section
  | ndb_section
  | fixes_section
  | airport_section
  | runway_section
  | sid_section
  | star_section
  | artcc_high_section
  | artcc_section
  | artcc_low_section
  | high_airway_section
  | low_airway_section
  | region_section
  | labels_section
  | geo_section
  | colour_definition
}

info_section = {
  "[INFO]"
  ~ (NL ~ colour_definition)*
  ~ NL ~ sector_name
  ~ NL ~ sector_callsign
  ~ NL ~ designator
  ~ NL ~ sct_coord_part
  ~ NL ~ sct_coord_part
  ~ NL ~ decimal
  ~ NL ~ decimal
  ~ NL ~ decimal
  ~ (NL ~ decimal)?
  ~ (NL ~ colour_definition)*
}
vor_section = { "[VOR]" ~ (NL ~ (colour_definition | location))* }
ndb_section = { "[NDB]" ~ (NL ~ (colour_definition | location))* }
fixes_section = { "[FIXES]" ~ (NL ~ (colour_definition | (fix | broken)))* }
airport_section = { "[AIRPORT]" ~ (NL ~ (colour_definition | aerodrome))* }
runway_section = { "[RUNWAY]" ~ (NL ~ (colour_definition | runway))* }
sid_section = { "[SID]" ~ (NL ~ (colour_definition | coloured_lines))* }
star_section = { "[STAR]" ~ (NL ~ (colour_definition | coloured_lines))* }
artcc_high_section = { "[ARTCC HIGH]" ~ (NL ~ (colour_definition | coloured_lines))* }
artcc_section = { "[ARTCC]" ~ (NL ~ (colour_definition | coloured_lines))* }
artcc_low_section = { "[ARTCC LOW]" ~ (NL ~ (colour_definition | coloured_lines))* }
high_airway_section = { "[HIGH AIRWAY]" ~ (NL ~ (colour_definition | airway))* }
low_airway_section = { "[LOW AIRWAY]" ~ (NL ~ (colour_definition | airway))* }
region_section = { ("[Regions]" | "[REGIONS]") ~ (NL ~ (colour_definition | region))* }
labels_section = { "[LABELS]" ~ (NL ~ (colour_definition | label))* }
geo_section = { "[GEO]" ~ (NL ~ (colour_definition | coloured_lines))* }

sector_name = @{ (LETTER | ASCII_DIGIT | "\t" | ' '..'~')+ }
sector_callsign = @{ (ASCII_ALPHANUMERIC | "_")+ }

colour_definition = { "#define" ~ colour_name ~ pos_integer }
colour_name = @{ (ASCII_ALPHANUMERIC | "-" | "_")+ }

fix = { fix_designator ~ sct_coordinate }
aerodrome = { designator ~ (frequency | ".")? ~ sct_coordinate ~ ctr_airspace }
location = { designator ~ frequency ~ sct_coordinate }
runway = { runway_designator ~ runway_designator ~ pos_integer ~ pos_integer ~ sct_coordinate ~ sct_coordinate ~ designator ~ (!NEWLINE ~ ANY)* }
airway = { fix_designator ~ (sct_coordinate | airway_fix) ~ (sct_coordinate | airway_fix) }

// invalid WPTs with spaces
broken = @{ !("[" | "#") ~ (!NL ~ ANY)* }

region = { "REGIONNAME"? ~ name ~ NL? ~ colour_name ~ sct_coordinate ~ (NL ~ sct_coordinate)* }

ctr_airspace = { "A" | "B" | "C" | "D" | "E" | "F" | "G" }

coloured_lines = { non_coordinate_or_double_fix_name ~ coloured_line ~ (NL ~ coloured_line)*  }
coloured_line = { (sct_coordinate | airway_fix) ~ (sct_coordinate | airway_fix) ~ colour_name? }

label = { quoted_string ~ sct_coordinate ~ colour_name }

// two identical consecutive fixes
airway_fix = ${ PUSH(designator) ~ WHITESPACE+ ~ POP }
sct_coordinate = ${ sct_coord_part ~ WHITESPACE+~ sct_coord_part }
frequency = @{ decimal }

name = @{ (!(NL | "\t" | "[") ~ ANY)* }
non_coordinate_name = { (!(sct_coordinate | NL) ~ ANY)* }
non_coordinate_or_double_fix_name = ${
  (!(
    ((WHITESPACE+ ~ sct_coordinate){2} ~ (WHITESPACE+ ~ colour_name)? ~ NL)
    | ((WHITESPACE+ ~ airway_fix){2} ~ (WHITESPACE+ ~ colour_name)? ~ NL)
    | "[" | "]"
  ) ~ ANY)+
}
fix_designator = @{ (LETTER | ASCII_DIGIT | "!" | "(" | ")" | "/" | "-" | "_" | "." | "[" | "]")+ }
designator = @{ (ASCII_ALPHA_UPPER | ASCII_DIGIT)+ }

COMMENT = _{ (";" | "//") ~ (!NEWLINE ~ ANY)* }
WHITESPACE = _{ " " | "\t" | "\u{feff}" }
