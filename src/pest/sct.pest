sct = { SOI ~ NL? ~ section* ~ EOI? }

section = _{
  info_section
  | vor_section
  | ndb_section
  | fixes_section
  | airport_section
  | runway_section
  | high_airway_section
  | low_airway_section
  | region_section
  | geo_section
  | unparsed_section
  | colour_definition
}

line = _{ broken ~ NL }

section_header = _{ "[" ~ header_name ~ "]" }

info_section = {
  "[INFO]" ~ NL ~ colour_definition* ~ sector_name ~ NL ~ sector_callsign ~ NL ~ designator ~ NL
  ~ sct_coord_part ~ NL ~ sct_coord_part ~ NL ~ decimal ~ NL ~ decimal ~ NL ~ decimal ~ NL
  ~ (decimal ~ NL)? ~ colour_definition*
}
vor_section = { "[VOR]" ~ NL ~ colour_definition* ~ (location ~ NL?)* ~ colour_definition* }
ndb_section = { "[NDB]" ~ NL ~ colour_definition* ~ ((location | broken) ~ NL)* ~ colour_definition* }
fixes_section = { "[FIXES]" ~ NL ~ colour_definition* ~ ((fix | broken) ~ NL)* ~ colour_definition* }
airport_section = { "[AIRPORT]" ~ NL ~ colour_definition* ~ (aerodrome ~ NL?)* ~ colour_definition* }
runway_section = { "[RUNWAY]" ~ NL ~ colour_definition* ~ (runway ~ NL)* ~ colour_definition* }
high_airway_section = { "[HIGH AIRWAY]" ~ NL ~ colour_definition* ~ (airway ~ NL)* ~ colour_definition* }
low_airway_section = { "[LOW AIRWAY]" ~ NL ~ colour_definition* ~ (airway ~ NL)* ~ colour_definition* }
region_section = { ("[Regions]" | "[REGIONS]") ~ NL ~ colour_definition* ~ region* ~ colour_definition* }
geo_section = { "[GEO]" ~ NL ~ colour_definition* ~ geo* ~ colour_definition* }
unparsed_section = { (section_header ~ NL ~ line*) }

sector_name = @{ (LETTER | ASCII_DIGIT | "\t" | ' '..'~')+ }
sector_callsign = @{ (ASCII_ALPHANUMERIC | "_")+ }


colour_definition = { "#define" ~ colour_name ~ pos_integer ~ NL }
colour_name = @{ (ASCII_ALPHANUMERIC | "-" | "_")+ }

fix = { fix_designator ~ sct_coordinate }
aerodrome = { designator ~ (frequency | ".")? ~ sct_coordinate ~ ("A" | "B" | "C" | "D" | "E" | "F" | "G") }
location = { designator ~ frequency ~ sct_coordinate }
runway = { runway_designator ~ runway_designator ~ pos_integer ~ pos_integer ~ sct_coordinate ~ sct_coordinate ~ designator ~ (!NEWLINE ~ ANY)* }
airway = { fix_designator ~ (sct_coordinate | airway_fix) ~ (sct_coordinate | airway_fix) }
// EDGG has invalid WPT with spaces
broken = @{ !("[" | "#") ~ (!NL ~ ANY)* }

region = { "REGIONNAME"? ~ name ~ NL? ~ colour_name ~ sct_coordinate ~ (NL ~ sct_coordinate)* ~ NL }

geo = { non_coordinate_name ~ geo_line_first ~ (NL ~ geo_line)* ~ NL  }
geo_line_first = { sct_coordinate ~ sct_coordinate ~ colour_name? }
geo_line = { (sct_coordinate | airway_fix) ~ (sct_coordinate | airway_fix) ~ colour_name? }

airway_fix = { designator ~ designator }
sct_coordinate = { sct_coord_part ~ sct_coord_part }
frequency = @{ decimal }

name = @{ (!(NL | "\t" | "[") ~ ANY)* }
non_coordinate_name = { (!(sct_coordinate | NL) ~ ANY)* }
fix_designator = @{ (LETTER | ASCII_DIGIT | "(" | ")" | "/" | "-" | "_" | "." | "[" | "]")+ }
designator = @{ (ASCII_ALPHA_UPPER | ASCII_DIGIT)+ }
header_name = @{ (ASCII_ALPHA_UPPER | WHITESPACE)+ }

COMMENT = _{ (";" | "//") ~ (!NEWLINE ~ ANY)* }
WHITESPACE = _{ " " | "\t" }
