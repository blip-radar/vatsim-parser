ese = { SOI ~ NL? ~ section+ ~ EOI}

section = _{
  position_section
  | airspace_section
  | sidsstars_section
  | unparsed_section
}

line = _{ broken ~ NL }

section_header = _{ "[" ~ header_name ~ "]" }

position_section = { "[POSITIONS]" ~ NL ~ (position ~ NL)* }
airspace_section = { "[AIRSPACE]" ~ NL ~ (sectorline | circle_sectorline | display_sectorline | sector | cop | fir_cop | msaw)* }
sidsstars_section = { "[SIDSSTARS]" ~ NL ~ ((sid | star) ~ NL)* }
unparsed_section = { (section_header ~ NL ~ line*) }

position = {
  text ~ ":" ~ text ~ ":" ~ frequency ~ ":" ~ text ~ ":" ~ text ~ ":" ~ text ~ ":"
  ~ text ~ ":" ~ text ~ ":" ~ text ~ ":" ~ squawk_range? ~ ":"?
  ~ coordinate? ~ (":" ~ coordinate)? ~ (":" ~ coordinate)? ~ (":" ~ coordinate)?
}
squawk_range = { INTEGER ~ ":" ~ INTEGER }

cop = {
  "COPX:" ~ prev ~ ":" ~ dep_rwy ~ ":" ~ cop_def ~ ":" ~ after ~ ":" ~ arr_rwy ~ ":" ~ sctx ~ ":"
  ~ sctn ~ ":" ~ alt_clb ~ ":" ~ alt_des ~ ":" ~ cop_description ~ NL
}
fir_cop = {
  "FIR_COPX:" ~ prev ~ ":" ~ dep_rwy ~ ":" ~ cop_def ~ ":" ~ after ~ ":" ~ arr_rwy ~ ":" ~ sctx ~ ":"
  ~ sctn ~ ":" ~ alt_clb ~ ":" ~ alt_des ~ ":" ~ cop_description ~ NL
}
prev = _{ wildcard | text }
dep_rwy = _{ wildcard | designator }
after = _{ wildcard | text }
arr_rwy = _{ wildcard | designator }
cop_def = _{ wildcard | text }
sctx = _{ text }
sctn = _{ text }
alt_clb = _{ wildcard | INTEGER }
alt_des = _{ wildcard | INTEGER }
cop_description = _{ text }

sectorline = { "SECTORLINE:" ~ text ~ NL ~ ((display | coord) ~ NL)* }
display = { "DISPLAY:" ~ text ~ ":" ~ text ~ ":" ~ text }

circle_sectorline = {
  "CIRCLE_SECTORLINE:" ~ text ~ ":" ~ fix_designator ~ ":" ~ DECIMAL ~ NL
  ~ (display ~ NL)*
}

display_sectorline = { "DISPLAY_SECTORLINE:" ~ text ~ ":" ~ text ~ ":" ~ text ~ ":" ~ text ~ NL }

sector = { "SECTOR:" ~ text ~ ":" ~ INTEGER ~ ":" ~ INTEGER ~ NL ~ (sector_subsettings ~ NL)* }
sector_subsettings = _{ owner | altowner | border | guest | active | depapt | arrapt }
owner = { "OWNER" ~ (":" ~ text)+ }
altowner = { "ALTOWNER:" ~ text ~ (":" ~ text)+ }
border = { "BORDER" ~ (":" ~ text)+ }
guest = { "GUEST" ~ ":" ~ sct ~ ":" ~ adep ~ ":" ~ ades  }
active = { "ACTIVE:" ~ designator ~ ":" ~ designator }
depapt = { "DEPAPT" ~ (":" ~ designator)+ }
arrapt = { "ARRAPT" ~ (":" ~ designator)+ }
sct = _{ text }
adep = _{ wildcard | designator }
ades = _{ wildcard | designator }

msaw = { "MSAW:" ~ text ~ ":" ~ INTEGER ~ NL ~ (coord ~ NL)+ }

sid = { "SID:" ~ designator ~ ":" ~ (rwy_designator | none) ~ ":" ~ text ~ ":" ~ route }
star = { "STAR:" ~ designator ~ ":" ~ (rwy_designator | none) ~ ":" ~ text ~ ":" ~ route }

wildcard = { "*" }

broken = @{ !("[") ~ (!NL ~ ANY)* }

coord = _{ "COORD:" ~ coordinate }
coordinate = { coord_part ~ ":" ~ coord_part }
coord_part = ${ hemi ~ deg ~ "." ~ min ~ "." ~ sec }

frequency = @{ DECIMAL }


route = { designator* }
fix_designator = @{ (LETTER | ASCII_DIGIT | "(" | ")" | "/" | "-" | "_" | ".")+ }
designator = @{ (ASCII_ALPHA_UPPER | ASCII_DIGIT | "-")+ }
pos_designator = @{ (ASCII_ALPHA_UPPER | ASCII_DIGIT | "_" | "-")+ }
rwy_designator = { ASCII_DIGIT{2} ~ ("L" | "C" | "R" | "G")? }
text = @{ (!(":" | NL) ~ ANY)* }
none = { "" }

header_name = @{ (ASCII_ALPHA_UPPER | WHITESPACE)+ }

deg = @{ ASCII_DIGIT+ }
min = @{ ASCII_DIGIT+ }
sec = @{ DECIMAL }
hemi = @{ "N" | "n" | "S" | "s" | "W" | "w" | "E" | "e" }

DECIMAL = @{ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? }
INTEGER = @{ ASCII_DIGIT+ }

NL = _{ NEWLINE+ }
COMMENT = _{ ";" ~ (!NEWLINE ~ ANY)* }
WHITESPACE = _{ " " | "\t" }
